import JavaBasics;
import Basics;

@advice(IIPEcosphere)
vilScript IIPEcosphere (Project source, Configuration config, Project target) {

    // preliminary!!! make it language-generic

    // java packages, turned to paths below
    String javaDatatypesPackage = "iip.datatypes";
    String javaSerializersPackage = "iip.serializers";
    String javaInterfacesPackage = "iip.interfaces";
    String javaStubsPackage = "iip.stubs";
    String javaNodesPackage = "iip.nodes";

    Path ecsRuntimeRoot = "${target}/ecsRuntime";
    Path serviceMgrRoot = "${target}/serviceMgr";
    Path platformRoot = "${target}/platform";
    
    main(Project source, Configuration config, Project target) = {
        
        setOf(RecordType) recordTypes = RecordType.allInstances();
        for (Application a : Application.allInstances()) {
            String appName = asTypeName(a.name);
            Path appRoot = "${target}/${appName}";
            appRoot.mkdir();
            Path javaSrc = "${appRoot}/src/main/java";
            Path pySrc = "${appRoot}/src/main/python";
            Path resourcesSrc = "${appRoot}/src/main/resources";
            Path assemblySrc = "${appRoot}/src/main/assembly";
            Path javaDatatypesSrc = "${javaSrc}/${toPath(javaDatatypesPackage)}";
            Path javaSerializersSrc = "${javaSrc}/${toPath(javaSerializersPackage)}";
            Path javaInterfacesSrc = "${javaSrc}/${toPath(javaInterfacesPackage)}";
            Path javaStubsSrc = "${javaSrc}/${toPath(javaStubsPackage)}";
            Path javaNodesSrc = "${javaSrc}/${toPath(javaNodesPackage)}";

            javaDatatypesSrc.mkdir();
            javaSerializersSrc.mkdir();
            // generate data classes and serializers
            setOf(String) javaSerializers = {};
            for (RecordType r : recordTypes) {
                // generate for all languages and according to serializer settings
                String clsName = asTypeName(r.name);
                vilTemplateProcessor("JavaType", config, "${javaDatatypesSrc}/${clsName}.java", type=r, pkg=javaDatatypesPackage);
                vilTemplateProcessor("JavaJsonSerializer", config, "${javaSerializersSrc}/${clsName}Serializer.java", type=r, pkg=javaSerializersPackage, typePkg=javaDatatypesPackage);
                javaSerializers.add("${javaSerializersPackage}.${clsName}Serializer");
                vilTemplateProcessor("PythonType", config, "${pySrc}/${clsName}.py", type=r);
                vilTemplateProcessor("PythonJsonSerializer", config, "${pySrc}/${clsName}Serializer.py", type=r);
            };

            // generate service classes and service integration
            setOf(String) artifacts = {};
            sequenceOf(NetElement) elements = {};
            artifacts.add(a.artifact);
            mapOf(NetElement, setOf(NetConnector)) mappedNet = {};
            // TODO filter according to assigned resources, allow for later instantiation
            for (ServiceNet n : a.services) {
                setOf(NetElement) nodes = n.sources->closure(NetElement e|nextNetNodes(e, mappedNet));
                for (NetElement no : nodes) {
                    String clsName = asTypeName(no.name);
                    vilTemplateProcessor("JavaNetElementInterface", config, "${javaInterfacesSrc}/${clsName}Service.java", elt=no, pkg=javaInterfacesPackage);
                    vilTemplateProcessor("JavaNetElementStub", config, "${javaStubsSrc}/${clsName}Stub.java", elt=no, pkg=javaStubsPackage);
                    vilTemplateProcessor("JavaSpringCloudStreamNetElement", config, "${javaNodesSrc}/${clsName}.java", elt=no, pkg=javaNodesPackage, app=a);
                    artifacts.add(getArtifact(no));
                    elements.add(no);
                };
            };
            
            vilTemplateProcessor("JavaStarter", config, "${javaSrc}/iip/Starter.java", pkg="iip", serializers=javaSerializers);

            vilTemplateProcessor("JavaSpringCloudStreamYaml", config, "${resourcesSrc}/application.yml", mappedNet=mappedNet);
            vilTemplateProcessor("SpringCloudStreamDeploymentDescriptor", config, "${resourcesSrc}/deployment.yml", app=a, elements=elements);

            assemblySrc.mkdir();
            vilTemplateProcessor("PythonAssembly", config, "${assemblySrc}/python.xml");
            vilTemplateProcessor("AppMvn", config, "${appRoot}/pom.xml", artifacts=artifacts, starterCls="iip.Starter", app=a, appName=appName);
            
            maven("${appRoot}");
        };
    
        ecsRuntimeRoot.mkdir();
        Path p = "${ecsRuntimeRoot}/src/main/resources";
        p.mkdir();
        vilTemplateProcessor("EcsRuntimeDockerContainerManagerYaml", config, "${p}/iipecosphere.yml");
        vilTemplateProcessor("EcsRuntimeMvn", config, "${ecsRuntimeRoot}/pom.xml");
        maven("${ecsRuntimeRoot}");
        
        serviceMgrRoot.mkdir();
        p = "${serviceMgrRoot}/src/main/resources";
        p.mkdir();
        vilTemplateProcessor("ServiceControlSpringCloudStreamYaml", config, "${p}/iipecosphere.yml");
        vilTemplateProcessor("ServiceControlMvn", config, "${serviceMgrRoot}/pom.xml");
        maven("${serviceMgrRoot}");
        
        platformRoot.mkdir();
        p = "${platformRoot}/src/main/resources";
        p.mkdir();
        vilTemplateProcessor("PlatformYaml", config, "${p}/iipecosphere.yml");
        vilTemplateProcessor("PlatformMvn", config, "${platformRoot}/pom.xml");
        maven("${platformRoot}");
        vilTemplateProcessor("WinBatch", config, "${target}/platform.bat");
    }

}