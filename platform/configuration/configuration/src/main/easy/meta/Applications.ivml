project Applications {

    import Devices;
    import Services;
    import DataTypes;
    
    annotate BindingTime bindingTime = BindingTime::compile to .;

    compound Application {
        Id id;
        NonEmptyString name;
        String description;
        IipVersion ver;
        String artifact = "";
        setOf(refTo(ServiceMesh)) services; // no services is ok, then the artifact must have hand-crafted services
    }
    
    compound ServiceMesh {
        String description;
        setOf(refTo(MeshSource)) sources;
    }
    
    compound MeshElement {
        NonEmptyString name;
        ServiceKind kind;
        refTo(ServiceBase) impl; // allow for family or service
        
        name == impl.name; // propagate
        kind == impl.kind; // propagate
    }
    
    // hint to requirements!
    compound MeshConnector refines CConnector {
        refTo(MeshInnerElement) next;
    }
    
    // TODO graph validation -> QM
    
    // TODO leave async, no polling so far, source-individual polling in Spring is a bit complicated
    compound MeshSource refines MeshElement {
        setOf(refTo(MeshConnector)) next;
        sequenceOf(IOType) output;
        output == impl.output;
        
        kind == ServiceKind::SOURCE_SERVICE;
    }
    
    abstract compound MeshInnerElement refines MeshElement {
        setOf(refTo(MeshConnector)) next;
    }
    
    compound MeshProcessor refines MeshInnerElement {
        sequenceOf(IOType) input;
        sequenceOf(IOType) output;
        input == impl.input;
        output == impl.output;

        kind == ServiceKind::TRANSFORMATION_SERVICE or kind == ServiceKind::PROBE_SERVICE;
    }
    
    compound MeshSink refines MeshInnerElement {
        sequenceOf(IOType) input;
        input == impl.input;

        kind == ServiceKind::SINK_SERVICE;
    }

}